algoritmo ConteudoDeTexto;
// Síntese: Algorítmo que analisa o conteúdo de um texto
//  Objetivo: analisar o texto e retornar informações acerca dele
//  Entrada : texto
//  Saída   : quantidade de caracteres, maiuscular minuscular, numerso, simbolos

principal
	texto textoInserido;
	leia(textoInserido);

	escreval("QUANTIDADE DE CARACTERES = ", qtdCaracteres(textoInserido));
	escreval("QUANTIDADE DE MAIUSCULAS = ", qtdMaiusculas(textoInserido));
	escreval("QUANTIDADE DE MINUSCULAS = ", qtdMinusculas(textoInserido));
	escreval("QUANTIDADE DE NUMEROS = ", qtdNumeros(textoInserido));
	escreval("QUANTIDADE DE SIMBOLOS = ", qtdSimbolos(textoInserido));
	
fimPrincipal

funcao inteiro qtdCaracteres(texto txt)
	retorna tamanhoTexto(txt);
fimFuncao

funcao inteiro qtdMaiusculas(texto txt)
	inteiro i, cont;
	caracter caractere_original, caractere_maiusculo;
	
	cont = 0;
	para (i de 0 ate qtdCaracteres(txt)-1 passo 1) faca
		caractere_original = caracterTexto(txt, i);
		caractere_maiusculo = maiusculoCaracter(caractere_original);
		se (caracterAscii(caractere_original)>=65 e caracterAscii(caractere_original)<=90) entao cont = cont + 1; fimSe
	fimPara
	retorna cont;
fimFuncao

funcao inteiro qtdMinusculas(texto txt)
	inteiro i, cont;
	caracter caractere_original, caractere_minusculo;
	
	cont = 0;
	para (i de 0 ate qtdCaracteres(txt)-1 passo 1) faca
		caractere_original = caracterTexto(txt, i);
		se (caracterAscii(caractere_original)>=97 e caracterAscii(caractere_original)<=122) entao cont = cont + 1; fimSe
	fimPara
	retorna cont;
fimFuncao

funcao inteiro qtdNumeros(texto txt)
	inteiro i, cont;
	caracter caractere_original;
	
	cont = 0;
	para (i de 0 ate qtdCaracteres(txt)-1 passo 1) faca
		caractere_original = caracterTexto(txt, i);
		se (caracterAscii(caractere_original)>=48 e caracterAscii(caractere_original)<=57) entao cont = cont + 1; fimSe
	fimPara
	retorna cont;
fimFuncao

funcao inteiro qtdSimbolos(texto txt)
	retorna qtdCaracteres(txt)-qtdMaiusculas(txt)-qtdMinusculas(txt)-qtdNumeros(txt);
fimFuncao
