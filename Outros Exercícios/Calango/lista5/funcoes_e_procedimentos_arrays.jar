procedimento exibirArrayInteiro(inteiro vetor[], inteiro tamanhoVetor)
  // Retorna a string "{elemento_1, elemento_2, ..., elemento_n}" de um array com n elementos.

	inteiro i;
	texto arrayFormatado;
	arrayFormatado = "";
	para (i de 0 ate tamanhoVetor-1 passo 1) faca
		se (i!=tamanhoVetor-1) entao arrayFormatado = arrayFormatado + vetor[i] + ", ";
		senao arrayFormatado = arrayFormatado + vetor[i]; fimSe
	fimPara
	arrayFormatado = "{" + arrayFormatado + "}";
	escreval(arrayFormatado);
fimProcedimento

funcao inteiro minArrayInteiro(inteiro vetor[], inteiro tamanhoVetor)
  //Retorna o valor mínimo de um array inteiro.

	inteiro i;
	inteiro min;
	para (i de 0 ate tamanhoVetor-1 passo 1) faca
		se (i==0) entao min = vetor[i];
		senao
			se (vetor[i] < min) entao min = vetor[i]; fimSe
		fimSe
	fimPara
	retorna min;
fimFuncao

funcao inteiro idxMinArrayInteiro(inteiro vetor[], inteiro tamanhoVetor)
  // Retorna o valor do índice correspondente ao menor valor do array inteiro.
	inteiro i;
	inteiro idxMin;

	para (i de 0 ate tamanhoVetor-1 passo 1) faca
		se (i==0) entao idxMin = 0;
		senao
			se (vetor[i] < vetor[idxMin]) entao idxMin = i; fimSe
		fimSe
	fimPara
	retorna idxMin;
fimFuncao
