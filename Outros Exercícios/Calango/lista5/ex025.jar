algoritmo classificacaoConcurso;
// Escreve na tela os nomes das melhores notas
//  Objetivo:  Em ordem decrescente printar os nomes dos melhores alunos
//  Entrada :  uma sequencia de notas e alunos
//  Saída   :  o ranking desejado por nós


principal
	texto aluno, nomes[100];
	inteiro N, notas[100], min, max, i;

	leia(N);
	para (i de 0 ate N-1 passo 1) faca
		leia(nomes[i]);
		leia(notas[i]);
	fimPara

	min = minArrayInteiro(notas, N);
	
	para (i de 0 ate N-1 passo 1) faca
		max = maxArrayInteiro(notas, N);
		aluno = nomes[idxMaxArrayInteiro(notas, N)];
		escreval(i+1 + " " + "-" + " " + aluno);
		notas[idxMaxArrayInteiro(notas, N)] = min-1;
	fimPara

fimPrincipal

funcao inteiro minArrayInteiro(inteiro vetor[], inteiro tamanhoVetor)
  //Retorna o valor mínimo de um array inteiro.

	inteiro i;
	inteiro min;
	para (i de 0 ate tamanhoVetor-1 passo 1) faca
		se (i==0) entao min = vetor[i];
		senao
			se (vetor[i] < min) entao min = vetor[i]; fimSe
		fimSe
	fimPara
	retorna min;
fimFuncao

funcao inteiro maxArrayInteiro(inteiro vetor[], inteiro tamanhoVetor)
  //Retorna o valor máximo de um array inteiro.

	inteiro i;
	inteiro max;
	para (i de 0 ate tamanhoVetor-1 passo 1) faca
		se (i==0) entao max = vetor[i];
		senao
			se (vetor[i] > max) entao max = vetor[i]; fimSe
		fimSe
	fimPara
	retorna max;
fimFuncao

funcao inteiro idxMaxArrayInteiro(inteiro vetor[], inteiro tamanhoVetor)
  // Retorna o valor do índice correspondente ao maior valor do array inteiro.
	inteiro i;
	inteiro idxMax;

	para (i de 0 ate tamanhoVetor-1 passo 1) faca
		se (i==0) entao idxMax = 0;
		senao
			se (vetor[i] > vetor[idxMax]) entao idxMax = i; fimSe
		fimSe
	fimPara
	retorna idxMax;
fimFuncao
